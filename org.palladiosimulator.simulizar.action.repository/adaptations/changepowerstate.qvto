import org.palladiosimulator.simulizar.action.repository.black.ProfilesLibrary;

modeltype INFRASTRUCTURE uses 'http://www.fzi.de/Power/Infrastructure/1.0';
modeltype POWER_BINDING uses 'http://www.fzi.de/Power/Binding/1.0';
modeltype ACTION_MAPPING uses 'http://simulizar.palladiosimulator.org/Actions/Mapping/1.0';
modeltype ACTION_CORE uses 'http://simulizar.palladiosimulator.org/Actions/Core/1.0';
modeltype ACTION_INSTANCE uses 'http://simulizar.palladiosimulator.org/Actions/Instance/1.0';

transformation changePowerState(
							inout powerInfrastructure : INFRASTRUCTURE,
							in actionRoleSet : ACTION_INSTANCE,
							in actionMapping : ACTION_MAPPING
							) {
		
							
	/**
	 * The main transformation.
	 * This is the entry point to the overall transformation.
	 */
	main() {	
		assert fatal(powerInfrastructure.rootObjects()[PowerInfrastructureRepository]->size() > 0)
			with log ("Infrastructure Repository not present!");
		var roleSet : RoleSet :=  actionRoleSet.rootObjects()[RoleSet]->any(true);
		var statefulPowerConsumingResources := powerInfrastructure.objectsOfType(StatefulPowerConsumingResource);
		var affectedStatefulPowerConsumingResource = roleSet.getAffectedStatefulPowerConsumingResourceFromRoles();
		assert fatal (statefulPowerConsumingResources[id = affectedStatefulPowerConsumingResource.id]->notEmpty())
			with log ("Inconsistency occurred!");
			
		affectedStatefulPowerConsumingResource->xmap changePowerState(roleSet.getDesiredPowerStateFromRoles());
	}
	
	mapping inout StatefulPowerConsumingResource::changePowerState(desiredPowerState : PowerState) {
		var binding := self.statefulResourcePowerBinding;
		if (binding.powerStates[id = desiredPowerState.id]->notEmpty()) {
			self.powerState := desiredPowerState;
		};	
	}
	
	query RoleSet::getDesiredPowerStateFromRoles() : PowerState {
		return self.roles[value.oclIsTypeOf(PowerState)]->any(true).value.oclAsType(PowerState);
	}
	
	query RoleSet::getAffectedStatefulPowerConsumingResourceFromRoles() : StatefulPowerConsumingResource {
		return self.roles[value.oclIsTypeOf(StatefulPowerConsumingResource)]->any(true).value.oclAsType(StatefulPowerConsumingResource);
	}
}
